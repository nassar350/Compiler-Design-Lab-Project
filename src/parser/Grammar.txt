Program        → DeclList EOF

DeclList       → Decl DeclList
               | ε

Decl           → VarDecl
               | FunDecl

VarDecl        → TypeSpec ID VarDeclTail ;

VarDeclTail    → , ID VarDeclTail
               | ε

FunDecl        → TypeSpec ID ( ParamList ) CompoundStmt

TypeSpec       → int
               | float
               | void

ParamList      → void
               | Param ParamListTail
               | ε

ParamListTail  → , Param ParamListTail
               | ε

Param          → TypeSpec ID

CompoundStmt   → { LocalDeclList StmtList }

LocalDeclList  → VarDecl LocalDeclList
               | ε

StmtList       → Stmt StmtList
               | ε

Stmt           → ExprStmt
               | CompoundStmt
               | IfStmt
               | WhileStmt
               | ForStmt
               | ReturnStmt

ExprStmt       → Expr ;
               | ;

IfStmt         → if ( Expr ) Stmt ElsePart

ElsePart       → else Stmt
               | ε

WhileStmt      → while ( Expr ) Stmt

ForStmt        → for ( ForInitExpr ; ForCondExpr ; ForIterExpr ) Stmt
//Yousef -> ForStmt        → for ( ExprStmt ExprStmt ExprOpt ) Stmt

ForInitExpr    → Expr
               | ε

ForCondExpr    → Expr
               | ε

ForIterExpr    → Expr
               | ε

ReturnStmt     → return ExprOpt ;

ExprOpt        → Expr
               | ε

Expr           → AssignExpr

AssignExpr     → ID = AssignExpr
               | OrExpr

OrExpr         → AndExpr OrExprTail

OrExprTail     → || AndExpr OrExprTail
               | ε

AndExpr        → RelExpr AndExprTail

AndExprTail    → && RelExpr AndExprTail
               | ε

RelExpr        → AddExpr RelOpTail

RelOpTail      → RelOp AddExpr
               | ε

RelOp          → <
               | <=
               | >
               | >=
               | ==
               | !=

AddExpr        → Term AddExprTail

AddExprTail    → + Term AddExprTail
               | - Term AddExprTail
               | ε

Term           → Factor TermTail

TermTail       → * Factor TermTail
               | / Factor TermTail
               | ε

Factor         → ( Expr )
               | ID FactorTail
               | Literal

FactorTail     → ( ArgList )
               | ε

ArgList        → Expr ArgListTail
               | ε

ArgListTail    → , Expr ArgListTail
               | ε

Literal        → INT_CONST
               | FLOAT_CONST